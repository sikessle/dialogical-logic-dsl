grammar de.htwg.msi.dialogicallogic.dsl.Logic hidden(WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate logic "http://www.htwg.de/msi/dialogicallogic/dsl/Logic" 


Model:
	aussagen+=Aussage+;
	
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal WS			: (' '|'\t'|'\r'|'\n')+;


Freitext:
	('ä' | 'Ä' | 'ö' | 'Ö' | 'ü' | 'Ü' | 'ß' | ID | WS)+
;

Variable:
	'«' name=Freitext '»';

Autor:
	name=Freitext
;

Aussage:
	name=Autor ':'
	inhalt=(ElementarAussage | VerbundeneAussage | Subjunktion | EinsQuantifizierteAussage |
	AllQuantifizierteAussage | Angriff | Verteidigung) '.';

ElementarAussage:
	'«' name=Freitext '»';

VerbundeneAussage:
	linkeAussage=ElementarAussage ('und' | 'oder') 
	rechteAussage=(ElementarAussage | VerbundeneAussage);

Subjunktion:
	('wenn' | 'weil') wennAussage=(ElementarAussage | VerbundeneAussage)
	('dann' | ('folgt' 'daraus')) dannAussage=(ElementarAussage | VerbundeneAussage);

EinsQuantifizierteAussage:
	'für' 'mindestens' ('einen' | 'eine' | 'ein') quantifiziertesElement=Variable
	'gilt:' betroffeneAussage=(ElementarAussage | VerbundeneAussage);

AllQuantifizierteAussage:
	'für' 'alle' quantifiziertesElement=Variable
	'gilt:' betroffeneAussage=(ElementarAussage | VerbundeneAussage);

Angriff:
	'Ich' 'bezweifle:'  
	'«' angegriffeneAussage=[ElementarAussage|Freitext] '»';	

Verteidigung:
	'Ich' 'beweise:' '«' verteidigteAussage=[ElementarAussage|Freitext] '»' 
	'durch:' beweis=(ElementarAussage | VerbundeneAussage);	
